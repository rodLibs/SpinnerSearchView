apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

// para usar .jar
//task sourceJar(type: Jar) {
//    from android.sourceSets.main.java.srcDirs
//}

def getVersionName = { ->
    return "1.4"
}

// Add the name of your library here
def getArtifactId = { ->
    return "spinnersearchview"
}

// Add the group ID of your library here
def getGroupId = { ->
    return "com.github.rodLibs"
}

// Prepare URL of maven package.
// Replace 'mohitrajput987' with your github repo's username or organization name.
// Replace 'analytics-sdk-android' with the name of github repo
def getGitHubUrl = { ->
    return "https://maven.pkg.github.com/rodLibs/SpinnerSearchView"
}


publishing {
    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/app-release.aar")

            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')
                ext.addDependency = { Dependency dep, String scope ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return
                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', scope)
                    if (!dep.transitive) {
                        final exclusionNode = dependencyNode
                                .appendNode('exclusions')
                                .appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    }
                    else if (!dep.properties.excludeRules.empty) {
                        final exclusionNode = dependencyNode
                                .appendNode('exclusions')
                                .appendNode('exclusion')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                } // end addDependency
                configurations.compile.getDependencies().each { dep ->
                    addDependency(dep, "compile")
                }
                configurations.api.getDependencies().each { dep ->
                    addDependency(dep, "compile")
                }
                configurations.implementation.getDependencies().each {dep ->
                    addDependency(dep, "runtime")
                }
            }

            // para usar .jar
//            artifact("$buildDir/libs/${getArtifactId()}.jar")
//            artifact(sourceJar)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri(getGitHubUrl())
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                println "GitHubPackages build.gradle\n\tusername=$username\n\ttoken=$password"
            }
        }
    }
}



android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
}


/*
    Depois de ter subido uma package para o gitHub
    Vai no site do jitpack: https://jitpack.io , loga com o git
    escolhe o repositório e vai ta lá a dependencia
 */